# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Aoibhinn Nic Aoidh <oibind@pm.me>
# Contributor: Michal Hybner <dta081@gmail.com>

pkgname=xtrlock
pkgver=2.13
pkgrel=1
pkgdesc='Minimal X display lock program'
url='https://packages.debian.org/sid/xtrlock'
arch=('x86_64')
license=('GPL3')
depends=('libx11')
source=(http://ftp.debian.org/debian/pool/main/x/xtrlock/xtrlock_${pkgver}.tar.xz
  noimage.patch)
sha512sums=('656e1e77194b816b04260a88c8046b8ef25d6df0405de6fe1ee1dfb2ebfa528a329c1b67be70eaac5ce4ee6a4159d728d363bf8a59232941b76b1e6d14aa91aa'
            '6b54f428eb77fa532c4c570c5f0948eaee7c7c76b48f531afdc6836b6855df6fbff2175ae2dce98416834b65cc678dd903060dca94f7a37c32e9b99f6d4d1cc3')

build() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/noimage.patch
  make -f Makefile.noimake \
    CFLAGS="${CFLAGS} -DSHADOW_PWD" \
    LDLIBS="-lX11 -lcrypt" \
    xtrlock
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 4755 xtrlock -t "${pkgdir}/usr/bin"
  install -Dm 644 xtrlock.man "${pkgdir}/usr/share/man/man1/xtrlock.1"
}

# vim: ts=2 sw=2 et:
